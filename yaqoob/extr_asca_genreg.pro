pro extr_asca_genreg, evtfil, srcreg, bgdreg, xybin=xybin, loglun=loglun
if n_params(0) eq 0 then begin
  print, 'extr_asca_genreg, evtfil, srcreg, bgdreg, xybin=xybin, ccds=ccds'
  print, 'Generate source and background regions based on evtfil'
  print, 'Intended to be called from extr_asca'
  retall
endif

if n_elements(xybin) eq 0 then xybin = 1
printf, loglun, 'Spatial binning factor = ' + strn(xybin)

qreadasca, evtfil, plist, h, gti
instr = strmid(sxpar(h, 'INSTRUME'), 0, 3)

; Bin image and get centroid of brightest pixel
dx = fix((plist.detx - 1)/xybin + 0.5)
dy = fix((plist.dety - 1)/xybin + 0.5)
sx = max(dx) + 1
sy = max(dy) + 1
im = intarr(sx, sy)
for i=0, n_elements(dx)-1 do im(dx(i), dy(i)) = im(dx(i), dy(i)) + 1 
imsm = gauss_smooth(im, 8.)
w = where(imsm eq max(imsm), c)
if c gt 1 then begin
  print, 'Warning... more than one maximum in smoothed image'
endif
w = w(0)

y = w/sx
x = w - y*sx
cntrd, imsm, x, y, xc, yc, 2.

; Now put (xc, yc) into fits convention
xc = xc + 1
yc = yc + 1
printf, loglun, 'Source centroid = (" + strn(xc) + ", " + strn(yc) + ")"

if instr eq 'SIS' then begin
  src = ' CIRCLE(' + strn(xc) + ', ' + strn(yc) + ', ' + strn(37.7*4/xybin) $
    + ')'
  w = where((dx - xc)^2 + (dy - yc)^2 lt (2.*xybin)^2, c)
  if c eq 0 then stop
  ccd = avg(plist(w).ccd)
  w = where(plist.ccd eq ccd)
  rx = minmax(dx(w))
  ry = minmax(dy(w))
  midx = avg(rx)
  midy = avg(ry)
  xsize = rx(1) - rx(0)
  ysize = ry(1) - ry(0)
  bgd = [' BOX(' + strn(midx) + ', ' + strn(midy) + ', ' + strn(xsize) + ', ' $
    + strn(ysize) + ')', '-CIRCLE(' + strn(xc) + ', ' + strn(yc) + ', ' + $
    strn(37.7*5/xybin) + ')']
endif else begin
  src = ' CIRCLE(' + strn(xc) + ', ' + strn(yc) + ', ' + strn(4.072*6/xybin) $
    + ')'
  bgd = [' CIRCLE(' + strn(xc) + ', ' + strn(yc) + ', ' + $
    strn(4.072*12/xybin) + ')', '-CIRCLE(' + strn(xc) + ', ' + strn(yc) + $
    ', ' + strn(4.072*7/xybin) + ')']
endelse

openw, lun, srcreg, /get_lun
printf, lun, '# Source region generated by extr_asca_genreg.pro on ' + $
  systime()
printf, lun, src
close, lun
openw, lun, bgdreg
printf, lun, '# Backgrd. region generated by extr_asca_genreg.pro on ' + $
  systime()
printf, lun, bgd(0)
printf, lun, bgd(1)
free_lun, lun

return
end
