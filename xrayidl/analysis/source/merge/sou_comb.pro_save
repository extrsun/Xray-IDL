pro sou_comb,flist,outfile,fdir=fdir,ftail=ftail,fsdir=fsdir,fstail=fstail,rlo=rlo,rhi=rhi,bflow=bflow
;+
; Combine sources from a list of observations into a single file
;
;*INPUTS:
; filelist - file list: each row contains an event file name
;
;*OUTPUTS:
; outfile - file name for the output parameters in a LaTex format
;
;*OPTIONAL Inputs:
; fdir - file directory (def ='../xdata/')
; ftail - characters appended to the filenames in filelist (e.g., '.fits')
; fsdir - source file directory, def = ''
; fstail - source file extension to the file names list in flist
;		def = '_map70_mlm_ratio.fits'
;
; running in sou directory: 
; sou_comb,'../imaging/flist.dat','sou_comb.fits'
;
; written by wqd 12/30/2001
;-
if n_params() eq 0 then begin
print,'CALL SEQUENCE - sou_comb,flist,outfile,fdir=fdir,ftail=ftail'
print,',fsdir=fsdir,fstail=fstail'
return
endif
if n_elements(fdir) eq 0 then fdir='../xdata/'
if n_elements(ftail) eq 0 then ftail='.fits'
if n_elements(fsdir) eq 0 then fsdir=''
if n_elements(fstail) eq 0 then fstail='_map70_mlm_ratio.fits'
if n_elements(rhi) eq 0 then rhi=15.*60
if n_elements(rlo) eq 0 then rlo=6.

;setup the source detection parameters:
if n_elements(instr) eq 0 then instr=!instr
;-----
;sources for subtraction:
if n_elements(bsoufile) ne 0 then begin
	sou_fits_info,bsoufile,slist,flow=bflow
	sou_det_params,instr,dim,block,ccd,emin,emax,bmin,bmax $
		,dsfrac,asfrac,psffiled,bpsffile ;,ftail,aimoff,subfac
	if n_elements(eband) ne 0 then emin=bmin(eband-1)
	if n_elements(eband) ne 0 then emax=bmax(eband-1)
	if n_elements(psffile) eq 0 then begin
		if n_elements(eband) ne 0 then psffile=bpsffile(eband-1) $
		else psffile=psffiled
	endif
	
	if n_elements(perclimit) eq 0 then perclimit=0.85
endif
;-------------

;get the total combined number of sources:
fname=''
k=0
nsv=[0]
openr,un,flist,/get
while not eof(un) do begin
	readf,un,fname
	shdr=headfits(fsdir+fname+fstail,ext=1)
	nsv=[nsv,nsv(k)+sxpar(shdr,'NAXIS2')]
	k=k+1
endwhile
free_lun,un
nob=k

nrow=nsv(nob)
openr,un,flist,/get
for k=0,nob-1 do begin
	readf,un,fname
	hdr=headfits(fdir+fname+ftail,ext=1)
	struct_s=mrdfits(fsdir+fname+fstail,1,shdr)
	;-----------
	print,fname
	file_params,fname,fhdr,fra,fdec 
		;,expt,nra,ndec,xoff,yoff,roll,hdr=hdr,aimoff=[0,0]
	sou_psf_remove,fra,fdec,struct_s,slist,rlo,rhi,perclimit=perc $
		,psffile=psffile,cntrth=cntrlimit,factor=factor
	;-------------
	if k eq 0 then begin
		struct_snew=create_struct(struct_s(0),'OBS_ID',0)
		struct_snew=replicate(struct_snew,nrow)
		ntag=n_tags(struct_s(0))
	endif
	for kk=0,ntag-1 do struct_snew(nsv(k):nsv(k+1)-1).(kk)=struct_s(*).(kk)
	struct_snew(nsv(k):nsv(k+1)-1).(ntag)=sxpar(hdr,'OBS_ID')
endfor
free_lun,un

; output the combined source structure:
mkhdr,h0,2 ;minimum herder
sxaddpar,h0,'CREATOR','sou_comb.pro'
sxaddpar,h0,'Comment: ','merged source list from files listed in '+flist
FXWRITE,outfile,h0
fxbhmake,h1,nrow,'srctable','created by sou_comb.pro (wqd)'
sxaddpar,h1,'EQUINOX',2000,'Default'
mwrfits,struct_snew,outfile,h1
return
end

